# Maintainer: Nicolas Chauvin <chauvin [dot] nico [at] gmail [dot] com>

pkgname=sam-solution-daemon-client
pkgver=r96.fed5113.rc2
pkgrel=1
pkgdesc="System Administration Manager Solution: Localhost controller"
arch=(any)
url="https://eip-sam.github.io/"
license=('LGPL3')
depends=('nodejs' 'git' 'libnotify')
makedepends=()
conflicts=('sam-solution-daemon-client-git')
provides=('sam-solution-daemon-client')
_pkgname=${pkgname}_${pkgver}_build_${arch}
source=("https://github.com/EIP-SAM/SAM-Solution-Daemon-Client/releases/download/${pkgver}/${_pkgname}.tar.gz")
sha256sums=('9b20484bd299787b498525f90fdf84d92ac1349e3c36355affe8338bfc56e7e7')

package() {
  msg2 "Creating target install directory..."
  mkdir -p "${pkgdir}/usr/share"

  msg2 "Creating system configuration directory..."
  mkdir -p "${pkgdir}/etc/${pkgname}"

  msg2 "Moving extracted folder to target directory..."
  mv "${_pkgname}" "${pkgdir}/usr/share/${pkgname}"

  msg2 "Linking configuration file to system configuration directory..."
  ln -s "/usr/share/${pkgname}/config/base.config.json" "${pkgdir}/etc/${pkgname}"

  msg2 "Copying systemd service configuration file..."
  install -Dm644 "${pkgdir}/usr/share/${pkgname}/system_config/sam-solution-daemon-client.service" \
    "${pkgdir}/usr/lib/systemd/system/sam-solution-daemon-client.service"

  msg2 "Updating package ownership to root:root..."
  chown -R root:root "${pkgdir}/"

  msg2 "Hiding readability of the configuration file for everyone except root..."
  chmod 600 "${pkgdir}/usr/share/${pkgname}/config/base.config.json"
}
